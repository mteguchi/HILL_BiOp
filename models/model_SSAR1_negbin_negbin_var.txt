# simple state space AR1 model for turtle nesting 
# original code from Lab 7, Introduction to Bayesian Time-Series
# analysis using Jags. Univ Washington: 
# https://www.scribd.com/document/373707632/Lab-7-Fitting-models-with-JAGS-pdf

# Also can be found here:
# https://nwfsc-timeseries.github.io/atsa-labs/sec-jags-uss.html
#
# For this model, I added another theta. Two thetas are used for increasing
# and decreasing months within each year. 

model{
	for (t in 2:T){
		predX[t] <- theta * X[t-1]

        # one variance for May - Sep, another for the rest
        var.pro[t] <- ifelse(m[t] < 10 && m[t] > 4, var.pro1, var.pro2)
        lambda.pro[t] <- r.X * (-1 + sqrt(1 + (4*var.pro[t])/r.X))

        p.X[t] <- r.X/(r.X + lambda.pro[t])
        X[t] ~ dnegbin(p.X[t], r.X)

		# observation
		y[t] ~ dnegbin(p[t], r)
        p[t] <- r/(r + X[t])
        
        loglik[t] <- log(dnegbin(y[t], p[t], r))
    }

    X[1] <- mu
    predY[1] <- X[1]
    y[1] ~ dnegbin(p[1], r)
    p[1] <- r/(r + X[1])

    mu ~ dpois(500)
    sigma.pro1 ~ dunif(0, 500)
    var.pro1 <- sigma.pro1 * sigma.pro1

    sigma.pro2 ~ dunif(0, 500)
    var.pro2 <- sigma.pro2 * sigma.pro2

    r ~ dgamma(0.1, 0.01)
    r.X ~ dgamma(0.1, 0.01)

    theta ~ dnorm(0, 0.01)
}