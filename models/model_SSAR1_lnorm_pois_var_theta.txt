# simple state space AR1 model for turtle nesting 
# original code from Lab 7, Introduction to Bayesian Time-Series
# analysis using Jags. Univ Washington: 
# https://www.scribd.com/document/373707632/Lab-7-Fitting-models-with-JAGS-pdf

# Also can be found here:
# https://nwfsc-timeseries.github.io/atsa-labs/sec-jags-uss.html
#
# For this model, I added another theta. Two thetas are used for increasing
# and decreasing months within each year. 

model{
	for (t in 2:T){
		# process: increasing before August, decreasing after July
        theta[t] <- ifelse(m[t] < 8, theta.1, theta.2)
        predX[t] <- theta[t] * X[t-1]

        # one variance for May - Sep, another for the rest
        tau.pro[t] <- ifelse(m[t] < 10 && m[t] > 4, tau.pro1, tau.pro2)
        X[t] ~ dlnorm(predX[t], tau.pro[t])

		# observation
		predY[t] <- exp(X[t])
		y[t] ~ dpois(exp(X[t]))

        loglik[t] <- log(dpois(y[t], exp(X[t])))
    }

    X[1] <- mu
    predY[1] <- exp(X[1])
    y[1] ~ dpois(exp(X[1]))

    mu ~ dnorm(0, 0.1)
    sigma.pro1 ~ dunif(0, 1)
    tau.pro1 <- 1/(sigma.pro1 * sigma.pro1)

    sigma.pro2 ~ dunif(0, 1)
    tau.pro2 <- 1/(sigma.pro2 * sigma.pro2)

    #sigma.obs ~ dunif(0, 100)
    #tau.obs <- 1/(sigma.obs * sigma.obs)

    theta.1 ~ dnorm(0, 1)
    theta.2 ~ dnorm(0, 1)
}