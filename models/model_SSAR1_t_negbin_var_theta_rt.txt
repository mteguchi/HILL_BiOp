# simple state space AR1 model for turtle nesting 
# original code from Lab 7, Introduction to Bayesian Time-Series
# analysis using Jags. Univ Washington: 
# https://www.scribd.com/document/373707632/Lab-7-Fitting-models-with-JAGS-pdf

# Also can be found here:
# https://nwfsc-timeseries.github.io/atsa-labs/sec-jags-uss.html
#
# For this model, I use Student's t distribution for the state process to accommodate 
# wider tails. There are two thetas for increasing
# and decreasing months within each year. And two variances for nesting and non-nesting 
# months. I use degrees of freedom = 4 rather than trying to estimate that parameter for 
# this. I also added time dependent r[t], or dispersion parameters. This was time 
# independent in previous models.

model{
	for (t in 2:T){
		# process: increasing before August, decreasing after July
        theta[t] <- ifelse(m[t] < 8, theta.1, theta.2)
        predX[t] <- theta[t] * X[t-1]

        # one variance for May - Sep, another for the rest
        tau.pro[t] <- ifelse(m[t] < 10 && m[t] > 4, tau.pro1, tau.pro2)
        X[t] ~ dt(predX[t], tau.pro[t], 4)T(0,)

		# observation
		y[t] ~ dnegbin(p[t], r[t])
        p[t] <- r[t]/(r[t] + X[t])
        r[t] ~ dnorm(0.0, 0.01)T(0,)
        loglik[t] <- logdensity.negbin(y[t], p[t], r[t])
        
    }

    X[1] <- mu
    y[1] ~ dnegbin(p[1], r[1])
    p[1] <- r[1]/(r[1] + X[1])
    
    mu ~ dnorm(400, 0.01)T(0,)
    r[1] ~ dnorm(0.0, 0.01)T(0,)

    sigma.pro1 ~ dgamma(0.1, 0.01)
    tau.pro1 <- 1/(sigma.pro1 * sigma.pro1)

    sigma.pro2 ~ dgamma(0.1, 0.01)
    tau.pro2 <- 1/(sigma.pro2 * sigma.pro2)

    #r ~ dgamma(0.1, 0.01)
    #r ~ dnorm(0.0, 0.01)T(0,)
    theta.1 ~ dnorm(0, 0.01)
    theta.2 ~ dnorm(0, 0.01)
}