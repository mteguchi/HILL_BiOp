# simple state space AR1 model for turtle nesting 
# original code from Lab 7, Introduction to Bayesian Time-Series
# analysis using Jags. Univ Washington: 
# https://www.scribd.com/document/373707632/Lab-7-Fitting-models-with-JAGS-pdf

# Also can be found here:
# https://nwfsc-timeseries.github.io/atsa-labs/sec-jags-uss.html
#
# For this model, I use Student's t distribution for the state process to accommodate 
# wider tails. One theta and one variance. 
# I use degrees of freedom = 4 rather than trying to estimate that parameter for 
# this. 

model{
	for (t in 2:T){
		# process: 
        predX[t] <- theta.1 * X[t-1]

        # one variance 
        X[t] ~ dt(predX[t], tau.pro1, 4)T(0,)

		# observation
		y[t] ~ dnegbin(p[t], r)
        p[t] <- r/(r + X[t])
        
        loglik[t] <- logdensity.negbin(y[t], p[t], r)
        
    }

    X[1] <- mu
    y[1] ~ dnegbin(p[1], r)
    p[1] <- r/(r + X[1])
    
    mu ~ dunif(350, 450)
    
    sigma.pro1 ~ dgamma(0.1, 0.01)
    tau.pro1 <- 1/(sigma.pro1 * sigma.pro1)

    r ~ dgamma(0.1, 0.01)

    theta.1 ~ dnorm(0, 0.01)
}